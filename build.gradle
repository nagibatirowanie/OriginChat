plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'com.gradleup.shadow' version '8.3.0'
}

group = 'me.nagibatirowanie'
version = '0.2.0-alpha.1'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "codemc"
        url = "https://repo.codemc.io/repository/maven-public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/releases/'
    }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
    maven { url = uri("https://repo.codemc.io/repository/maven-releases/") }
    maven { 
      name = "jitpack"
      url = "https://jitpack.io/"
    }
    
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT")
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'com.comphenix.protocol:ProtocolLib:5.1.0'
    compileOnly 'com.github.decentsoftware-eu:decentholograms:2.8.17'

    implementation 'fr.mrmicky:fastboard:2.1.4'
    implementation 'com.tchristofferson:ConfigUpdater:2.2-SNAPSHOT'
    
    // Добавляем Netty для ByteBuf
    implementation 'io.netty:netty-buffer:4.1.107.Final'
    implementation 'io.netty:netty-codec:4.1.107.Final'
    
    //implementation("org.ahocorasick:ahocorasick:0.4.0")
    implementation("io.github.milkdrinkers:colorparser:3.0.1")
    //implementation("com.github.retrooper:packetevents-spigot:2.7.0")
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21")
    }
    
    // Настройка Shadow JAR для включения зависимостей и переименования пакетов
    shadowJar {
        //relocate('org.ahocorasick', 'me.nagibatirowanie.originchat.libs.ahocorasick')
        relocate 'fr.mrmicky.fastboard', 'me.nagibatirowanie.originchat.libs.fastboard'
        relocate 'com.tchristofferson.configupdater', 'me.nagibatirowanie.originchat.libs.configupdater'
        //relocate 'com.github.retrooper','me.nagibatirowanie.originchat.libs.packetevents'
        
        // Перемещаем классы Netty в наше пространство имен
        relocate 'io.netty', 'me.nagibatirowanie.originchat.libs.netty'
        relocate("io.github.milkdrinkers.colorparser", "me.nagibatirowanie.originchat.libs.colorparser")

        minimize()
        
        archiveClassifier.set('')
    }
    
    // Привязываем задачу shadowJar к задаче build
    build {
        dependsOn(shadowJar)
    }
}

def targetJavaVersion = 21
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}